// build.gradle (root)

description = 'Simple text to image converter'

ext {
   // location to store all projects' dependencies
   rootLib = file(rootProject.projectDir.path + '/lib')
}

allprojects {
   apply plugin: 'java'
   apply plugin: 'eclipse'
   
   group = 'kkdt'
   version = '0.1'
   
   eclipse {
      classpath {
         downloadSources=false
      }
   }
   
   repositories {
      jcenter()
   }
   
   task jarSources(type: Jar, dependsOn: classes, description:'Generate source artifacts') {
      from sourceSets.main.allSource
      classifier = 'src'
      destinationDir = rootLib
   }
   
   task jarJavadocs(type: Jar, dependsOn: javadoc, description:'Generate Javadoc artifacts') {
      from javadoc.destinationDir
      classifier = 'javadoc'
      destinationDir = rootLib
   }
   
   artifacts {
      archives jar
      archives jarSources
   }
   
   task cleanAll(description: 'Clean all artifacts created by this project build') {
      doLast {
         delete rootProject.rootLib
      }
   }
   cleanAll.dependsOn 'clean'
   
   tasks.withType(Jar) {
      destinationDir = rootLib
   }
   
   tasks.withType(Zip) {
      destinationDir = rootLib
   }
}

dependencies {
   testCompile 'junit:junit:4.12'
}

// https://github.com/kkdt/gradle-java-multiprojects
if(file('eclipseone.gradle').exists()) {
   apply from : 'eclipseone.gradle'
}

task all(dependsOn:['jar','jarSources', ':lambda:jar', ':lambda:jarSources', ':lambda:zipLambda'], description: 'Build all artifacts') {}

//distributions {
//   main {
//      baseName = 'txt2img'
//      contents {
//         from compileJava
//         from processResources
//         into('lib') { from configurations.runtime }
//      }
//   }
//}
